<програма>::=main ( <список_оголошень1> ) { <список_оп-ів1> }
<список_оголошень1>::=<список_оголошень>
<список_оп-ів1>::=<список_оп-ів>
<список_оголошень>::=<оголошення> ; <список_оголошень>/ <оголошення> ;
<оголошення>::=int <список_змінних>/ char <список_змінних>/ bool <список_змінних>
<список_змінних>::=<список_змінних> , id/ id 
<список_оп-ів>::=<оп1> ;/ <оп1> ; <список_оп-ів>
<оп1>::=<оп>
<оп>::=<присв>/ scanf <інд> . id/ printf <інд> . id/ printf <інд> . lit/ printf <інд> . con/for id = <вираз1> to <вираз1> by <вираз1> while <лог_вираз1> { <список_оп-ів1> } end/ if <відн> then <оп>
<присв>::=id = <вираз1>/ id = lit
<інд>::=%i/ %c/ %b
<вираз1>::=<вираз>
<вираз>::=<терм1>/ - <терм1>/ <вираз> + <терм1>/ <вираз> – <терм1>
<терм1>::=<терм>
<терм>::=<множ1>/ <терм> * <множ1>/ <терм> $ <множ1>
<множ1>::=<множ>
<множ>::=<перв1>/ <множ> ^ <перв1>
<перв1>::=<перв>
<перв>::=id/ con/ ( <вираз1> )
<лог_вираз>::=<ЛТ1>/ <лог_вираз> || <ЛТ1>
<ЛТ1>::=<ЛТ>
<ЛТ>::=<ЛМ>/ <ЛТ> && <ЛМ>
<лог_вираз1>::=<лог_вираз>
<ЛМ>::=<відн>/ !! <ЛМ>/ [ <лог_вираз1> ]
<відн>::=<вираз> <знак_відн> <вираз1>/ true/ false
<знак_відн>::=!=/ >/ </ <=/ >=/ ==